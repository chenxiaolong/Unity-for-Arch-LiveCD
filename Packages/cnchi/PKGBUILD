# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>
# Original Maintainer: Alex Filgueira <alexfilgueira@antergos.com>

_commit=8c672a125d13332bf5b99c3e1b57a0dea412da27
_tagver=0.2.1

pkgname=cnchi
pkgver=0.2.1.r504.8c672a1
pkgrel=2
pkgdesc='Graphical Installer for Antergos Linux'
arch=('any')
license=('GPL')
url="https://github.com/Antergos/Cnchi"
depends=('pm2ml' 'python' 'pyalpm' 'python-gobject' 'python-dbus' 'hdparm' 'python-cairo' 'libtimezonemap' 'py3parted' 'hwinfo' 'webkitgtk3')
makedepends=('git')
source=("Cnchi::git+https://github.com/Antergos/Cnchi.git"
        '0001-Modifications-for-Unity-for-Arch.patch'
        'cnchi.desktop'
        'cnchi.bin'
        'cnchi.real.sbin'
        'to.epac.cxl.pkexec.cnchi.policy'
        'unity.png')
sha512sums=('SKIP'
            '8ebc169dad01edcfec62ced0ea89fa7797380871ea1e7f9ec2011da2564721de91289d51186b79fee653e6a0602100513b6a1a8c91789dac16d6266854ad0e03'
            '4ebdb8a2c1298a7fb3b06dbbdf475205fc1e6880d82310c1b98a2d776e81c9ba88b218bee67b8c1a423ec17fc5ed522ab4dd42291f22cccb1621bbc0f9dd1c3e'
            '06c3eac336d5ff48d7653f5a2db883a45dc0dccae536a7d6f3d986e1032aa597b557d20be09e1c8f3d07b87e3d8d9b70aaff9c5479289b026f3576150e043a5c'
            '54410a89aea637c1dff1df56ca46bd93e3678ff5242197879eb9e8d8b51572af67717bb9812ee10296358c5c8b11736d9a64a4967e49baa5381d78ca9aac04de'
            'b2c16569ef6d0601328f4a3f709f0a3f5c1d64b5914e7aeed2dfbef11f0d45b512a8a1cb7355e124a540d61a56e63deb0406be947c5f1efe5ce4bedf42998585'
            '670e38f76e681401b4dd30366f351fedb1082171804aec0a057a161b1f46ba5a67d9c655ccc5f0a837ef7396c1c8f1aba4e25b92eaa6c4851bb770974387fec6')

pkgver() {
  cd "${srcdir}/Cnchi/"

  # Specifying the commit in the source line does not work
  git checkout "${_commit}"

  echo "${_tagver}.r$(git rev-list --count HEAD).$(git rev-parse --short HEAD)"
}

prepare() {
  cd "${srcdir}/Cnchi/"

  patch -p1 -i "${srcdir}/0001-Modifications-for-Unity-for-Arch.patch"
}

package() {
  cd "${srcdir}/Cnchi/"
  
  install -dm755 "${pkgdir}/usr/share/cnchi/"
  install -m755 cnchi.py "${pkgdir}/usr/share/cnchi/"

  for i in data scripts src ui; do
    cp -R ${i} "${pkgdir}/usr/share/cnchi/"
  done

  install -m644 "${srcdir}/unity.png" "${pkgdir}/usr/share/cnchi/data/desktops/"

  install -dm755 "${pkgdir}/usr/share/applications/"
  install -m644 "${srcdir}/cnchi.desktop" "${pkgdir}/usr/share/applications/"

  install -dm755 "${pkgdir}/etc/xdg/autostart/"
  install -m644 "${srcdir}/cnchi.desktop" "${pkgdir}/etc/xdg/autostart/"
  sed -i 's/^\(Exec.*\)/\1 --autostart/g' \
    "${pkgdir}/etc/xdg/autostart/cnchi.desktop"

  install -dm755 "${pkgdir}/usr/bin/"
  install -m755 "${srcdir}/cnchi.bin" "${pkgdir}/usr/bin/cnchi"

  install -dm755 "${pkgdir}/usr/sbin/"
  install -m755 "${srcdir}/cnchi.real.sbin" "${pkgdir}/usr/sbin/cnchi.real"

  install -dm755 "${pkgdir}/usr/share/pixmaps/"
  ln -s /usr/share/cnchi/data/icons/128x128/cnchi.png \
        "${pkgdir}/usr/share/pixmaps/"

  install -dm755 "${pkgdir}/usr/share/polkit-1/actions/"
  install -m644 "${srcdir}/to.epac.cxl.pkexec.cnchi.policy" \
                "${pkgdir}/usr/share/polkit-1/actions/"
}
