# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>
# Original Maintainer: Alex Filgueira <alexfilgueira@antergos.com>

_commit=86a617394ac92629ecd067939f2b6a6bcf15cee8
_tagver=0.2.1

pkgname=cnchi
pkgver=0.2.1.r341.86a6173
pkgrel=2
pkgdesc='Graphical Installer for Antergos Linux'
arch=('any')
license=('GPL')
url="https://github.com/Antergos/Cnchi"
depends=('python' 'pyalpm' 'python-gobject' 'python-dbus' 'hdparm' 'python-cairo' 'libtimezonemap' 'py3parted' 'hwinfo' 'webkitgtk3')
makedepends=('git')
source=("Cnchi::git+https://github.com/Antergos/Cnchi.git#${commit}"
        '0001-Modifications-for-Unity-for-Arch.patch'
        'cnchi.desktop'
        'cnchi.bin'
        'cnchi.real.sbin'
        'to.epac.cxl.pkexec.cnchi.policy')
sha512sums=('SKIP'
            'dff1da692eecb36d1cb61ddebc66caec2934a7b74741b0633cb3e736890abfbf730455962065a28d9eaaca366c16d7a376d410344ca5fb7f24d712bd5efb4e6e'
            '4ebdb8a2c1298a7fb3b06dbbdf475205fc1e6880d82310c1b98a2d776e81c9ba88b218bee67b8c1a423ec17fc5ed522ab4dd42291f22cccb1621bbc0f9dd1c3e'
            '8dec17190db9c0460f4008bcf8949f1de7a8fabf92f57f139015c6e590c81a1ee4f8bda23464322250e59cb77eaa85eb2c8005f1712c322124b6a6fd807af6ee'
            'd13b1c93778ecda62ba3f58d953ae43891378786c434382b03282725d80f5afb53472173f86c744b3dccb13a0536e9dcf1403c39bfa46a90503a62deb61e1389'
            'b2c16569ef6d0601328f4a3f709f0a3f5c1d64b5914e7aeed2dfbef11f0d45b512a8a1cb7355e124a540d61a56e63deb0406be947c5f1efe5ce4bedf42998585')

pkgver() {
  cd "${srcdir}/Cnchi/"
  echo "${_tagver}.r$(git rev-list --count HEAD).$(git rev-parse --short HEAD)"
}

prepare() {
  cd "${srcdir}/Cnchi/"

  patch -p1 -i "${srcdir}/0001-Modifications-for-Unity-for-Arch.patch"
}

package() {
  cd "${srcdir}/Cnchi/"
  
  install -dm755 "${pkgdir}/usr/share/cnchi/"
  install -m755 cnchi.py "${pkgdir}/usr/share/cnchi/"

  for i in data scripts src ui; do
    cp -R ${i} "${pkgdir}/usr/share/cnchi/"
  done

  cp "${pkgdir}"/usr/share/cnchi/data/desktops/{gnome,unity}.png

  install -dm755 "${pkgdir}/usr/share/applications/"
  install -m644 "${srcdir}/cnchi.desktop" "${pkgdir}/usr/share/applications/"

  install -dm755 "${pkgdir}/etc/xdg/autostart/"
  install -m644 "${srcdir}/cnchi.desktop" "${pkgdir}/etc/xdg/autostart/"
  sed -i 's/^\(Exec.*\)/\1 --autostart/g' \
    "${pkgdir}/etc/xdg/autostart/cnchi.desktop"

  install -dm755 "${pkgdir}/usr/bin/"
  install -m755 "${srcdir}/cnchi.bin" "${pkgdir}/usr/bin/cnchi"

  install -dm755 "${pkgdir}/usr/sbin/"
  install -m755 "${srcdir}/cnchi.real.sbin" "${pkgdir}/usr/sbin/cnchi.real"

  install -dm755 "${pkgdir}/usr/share/pixmaps/"
  ln -s /usr/share/cnchi/data/icons/128x128/cnchi.png \
        "${pkgdir}/usr/share/pixmaps/"

  install -dm755 "${pkgdir}/usr/share/polkit-1/actions/"
  install -m644 "${srcdir}/to.epac.cxl.pkexec.cnchi.policy" \
                "${pkgdir}/usr/share/polkit-1/actions/"
}
